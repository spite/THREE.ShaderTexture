<!doctype html>
<html lang="en">
	<head>
		<title>Procedurally Textured Torus</title>
		<meta charset="utf-8">
		<meta name="viewport" content="width=device-width, user-scalable=no, minimum-scale=1.0, maximum-scale=1.0">

		<link href="https://fonts.googleapis.com/css?family=Lato:100,700|Playfair+Display:700" rel="stylesheet">
		<style>
*{
	box-sizing: border-box;
	margin :0;
	padding: 0;
}
html {
	height: 100%;
	-moz-user-select: none;
	-khtml-user-select: none;
	-webkit-user-select: none;
	-ms-user-select: none;
	user-select: none;
}
body {
	margin: 0;
	padding: 0;
	background-color: #202020;
	color: #ffffff;
	line-height: 1.4em;
	height: 100%;
	overflow: hidden;
	font-family: 'Lato', sans-serif;
	font-size: 13px;
	font-weight: 100;
	text-shadow: 0 1px 0 rgba(0,0,0,1);
}
#container {
	position: absolute;
	left: 0;
	top: 0;
	right: 0;
	bottom: 0;
	width: 100%;
	height: 100%;
}
		</style>

	</head>
	<body>

		<div id="container"></div>

		<script src="js/three.js"></script>
		<script src="js/OrbitControls.js"></script>
		<script src="../src/THREE.ShaderTexture.js"></script>

<script type="x-shader/x-vertex" id="ortho-vs" >
precision highp float;

attribute vec3 position;
attribute vec2 uv;

uniform mat4 modelViewMatrix;
uniform mat4 projectionMatrix;

varying vec2 vUv;

void main() {

	vUv = uv;
	gl_Position = projectionMatrix * modelViewMatrix * vec4( position, 1. );

}
</script>

<script id="texture-fs" type="x-shader/x-fragment">
precision highp float;

uniform float type;

varying vec2 vUv;

#define EPS vec2(1e-3, 0.0)
#define ON vec2(1.0, 0.0)

float N(vec2 p)
{
	p = mod(p, vec2(101.0));
	return fract(sin(p.x * 41784.0) + sin(p.y * 32424.0));
}

float smN2(vec2 p)
{
	vec2 fp = floor(p);
	vec2 pf = smoothstep(0.0, 1.0, fract(p));

	return mix( mix(N(fp), N(fp + ON), pf.x),
			   mix(N(fp + ON.yx), N(fp + ON.xx), pf.x), pf.y);
}

float smN3(vec3 p)
{
	vec2 o = vec2(111.0);
	return mix(smN2(p.xy + floor(p.z) * o),
			   smN2(p.xy + (floor(p.z) + 1.0) * o), smoothstep(0.0, 1.0, fract(p.z)));
}

float fbm3(vec3 p)
{
	float f = 0.0, x;
	for(int i = 1; i <= 7; ++i)
	{
		x = exp2(float(i));
		f += (smN3(p * x) - 0.5) / x;
	}
	return f;
}

#define fbm2(g) fbm3(vec3(g, 0.0))

float random(vec2 n, float offset ){
	//return fract(sin(dot(gl_FragCoord.xyz+seed,scale))*43758.5453);
	return .5 - fract(sin(dot(n.xy + vec2( offset, 0. ), vec2(12.9898, 78.233)))* 43758.5453);
}

vec3 bump(vec2 p)
{
	float c = .5 + .5 * fbm2(p);
	float s = .5 + .5 * sin( 10. * p.y );
	c *= .45 + .55 * s;
	c += .01 * random(p,.234243);
    return vec3(c);
}

vec3 diffuse(vec2 p)
{
    float c = .5;
    float s = .5 + .5 * sin( 10. * p.y );
	s = smoothstep( .35, .65, s );
	c *= .25 + .75 * s;
	c += .01 * random(p,.45673);
	float r = 1. - smoothstep(.25,.75,c);
	float g = smoothstep(.25,.75,c);
    return vec3(r,.5 * g + .5 * r,g);

}

vec3 roughness(vec2 p)
{
	float c = .5;
	float s = .5 + .5 * sin( 10. * p.y );
	s = smoothstep( .35, .65, s );
	c *= .25 + .75 * s;
	c *= .01 * random(p,.7556);
	return vec3(1.-c);

}

void main()
{
	float r = 4.;
	vec2 uv = vUv;

	if( type == 0. ){
		gl_FragColor = vec4(diffuse(r*uv),1.0);
	}
	if( type == 1. ) {
		gl_FragColor = vec4(bump(r*uv),1.0);
	}
	if( type == 2. ) {
		gl_FragColor = vec4(roughness(r*uv),1.0);
	}
}

</script>

<script id="roughness-fs" type="x-shader/x-fragment">
precision highp float;

uniform sampler2D diffuseMap;
uniform vec2 dimensions;
uniform float seed;

varying vec2 vUv;

float hash( float n ) { return fract(sin(n)*753.5453123); }
float noise( in vec3 x )
{
	vec3 p = floor(x);
	vec3 f = fract(x);
	f = f*f*(3.0-2.0*f);

	float n = p.x + p.y*157.0 + 113.0*p.z;
	return mix(mix(mix( hash(n+  0.0), hash(n+  1.0),f.x),
	               mix( hash(n+157.0), hash(n+158.0),f.x),f.y),
	mix(mix( hash(n+113.0), hash(n+114.0),f.x),
	    mix( hash(n+270.0), hash(n+271.0),f.x),f.y),f.z);
}

float luma(vec3 color) {
  return dot(color, vec3(0.299, 0.587, 0.114));
}

void main()
{
	vec2 uv = vUv;
	vec3 c = texture2D( diffuseMap, vUv ).rgb;
	float l = 2. * luma( c ) + 1. * noise( vec3(10.*dimensions*(uv+c.r),seed));
	gl_FragColor = vec4( vec3(l), 1. );

}

</script>

<script id="blur-fs" type="x-shader/x-fragment">
precision highp float;

uniform sampler2D tInputMap;
uniform vec2 dimensions;
uniform float sigma;

varying vec2 vUv;

float normpdf(in float x, in float sigma)
{
	return 0.39894*exp(-0.5*x*x/(sigma*sigma))/sigma;
}

void main() {

	//declare stuff
	const int mSize = 11;
	const int kSize = (mSize-1)/2;
	float kernel[mSize];
	vec3 final_colour = vec3(0.0);

	//create the 1-D kernel
	float Z = 0.0;
	for (int j = 0; j <= kSize; ++j)
	{
		kernel[kSize+j] = kernel[kSize-j] = normpdf(float(j), sigma);
	}

	//get the normalization factor (as the gaussian has been clamped)
	for (int j = 0; j < mSize; ++j)
	{
		Z += kernel[j];
	}

	//read out the texels
	for (int i=-kSize; i <= kSize; ++i)
	{
		for (int j=-kSize; j <= kSize; ++j)
		{
			final_colour += kernel[kSize+j]*kernel[kSize+i]*texture2D(tInputMap, (gl_FragCoord.xy+vec2(float(i),float(j))) / dimensions.xy).rgb;

		}
	}

	gl_FragColor = vec4(final_colour/(Z*Z), 1.0);

}

</script>

<script id="heightmap-to-normalmap-fs" type="x-shader/x-fragment">
// from http://cpetry.github.io/NormalMap-Online/
precision highp float;
precision highp int;
#define SHADER_NAME HeightMapToNormalMap

varying vec2 vUv;

uniform vec2 dimensions;
uniform float dz;
uniform float invertR;
uniform float invertG;
uniform float invertH;
uniform int type;
uniform sampler2D tHeightMap;

void main(void) {

	vec2 step = vec2(-1.0 / dimensions.x, -1.0 / dimensions.y);

	vec2 tlv = vec2(vUv.x - step.x, vUv.y + step.y );
	vec2 lv  = vec2(vUv.x - step.x, vUv.y 		   );
	vec2 blv = vec2(vUv.x - step.x, vUv.y - step.y);
	vec2 tv  = vec2(vUv.x 		  , vUv.y + step.y );
	vec2 bv  = vec2(vUv.x 		  , vUv.y - step.y);
	vec2 trv = vec2(vUv.x + step.x, vUv.y + step.y );
	vec2 rv  = vec2(vUv.x + step.x, vUv.y 		   );
	vec2 brv = vec2(vUv.x + step.x, vUv.y - step.y);
	tlv = vec2(tlv.x >= 0.0 ? tlv.x : (1.0 + tlv.x), 	tlv.y >= 0.0	? tlv.y : (1.0  + tlv.y));
	tlv = vec2(tlv.x < 1.0  ? tlv.x : (tlv.x - 1.0 ), 	tlv.y < 1.0   	? tlv.y : (tlv.y - 1.0 ));
	lv  = vec2( lv.x >= 0.0 ?  lv.x : (1.0 + lv.x),  	lv.y  >= 0.0 	?  lv.y : (1.0  +  lv.y));
	lv  = vec2( lv.x < 1.0  ?  lv.x : ( lv.x - 1.0 ),   lv.y  < 1.0  	?  lv.y : ( lv.y - 1.0 ));
	blv = vec2(blv.x >= 0.0 ? blv.x : (1.0 + blv.x), 	blv.y >= 0.0 	? blv.y : (1.0  + blv.y));
	blv = vec2(blv.x < 1.0  ? blv.x : (blv.x - 1.0 ), 	blv.y < 1.0 	? blv.y : (blv.y - 1.0 ));
	tv  = vec2( tv.x >= 0.0 ?  tv.x : (1.0 + tv.x),  	tv.y  >= 0.0 	?  tv.y : (1.0  +  tv.y));
	tv  = vec2( tv.x < 1.0  ?  tv.x : ( tv.x - 1.0 ),   tv.y  < 1.0 	?  tv.y : ( tv.y - 1.0 ));
	bv  = vec2( bv.x >= 0.0 ?  bv.x : (1.0 + bv.x),  	bv.y  >= 0.0 	?  bv.y : (1.0  +  bv.y));
	bv  = vec2( bv.x < 1.0  ?  bv.x : ( bv.x - 1.0 ),   bv.y  < 1.0 	?  bv.y : ( bv.y - 1.0 ));
	trv = vec2(trv.x >= 0.0 ? trv.x : (1.0 + trv.x), 	trv.y >= 0.0 	? trv.y : (1.0  + trv.y));
	trv = vec2(trv.x < 1.0  ? trv.x : (trv.x - 1.0 ), 	trv.y < 1.0   	? trv.y : (trv.y - 1.0 ));
	rv  = vec2( rv.x >= 0.0 ?  rv.x : (1.0 + rv.x),  	rv.y  >= 0.0 	?  rv.y : (1.0  +  rv.y));
	rv  = vec2( rv.x < 1.0  ?  rv.x : ( rv.x - 1.0 ),   rv.y  < 1.0   	?  rv.y : ( rv.y - 1.0 ));
	brv = vec2(brv.x >= 0.0 ? brv.x : (1.0 + brv.x), 	brv.y >= 0.0 	? brv.y : (1.0  + brv.y));
	brv = vec2(brv.x < 1.0  ? brv.x : (brv.x - 1.0 ), 	brv.y < 1.0   	? brv.y : (brv.y - 1.0 ));
	float tl = abs(texture2D(tHeightMap, tlv).r);
	float l  = abs(texture2D(tHeightMap, lv ).r);
	float bl = abs(texture2D(tHeightMap, blv).r);
	float t  = abs(texture2D(tHeightMap, tv ).r);
	float b  = abs(texture2D(tHeightMap, bv ).r);
	float tr = abs(texture2D(tHeightMap, trv).r);
	float r  = abs(texture2D(tHeightMap, rv ).r);
	float br = abs(texture2D(tHeightMap, brv).r);
	float dx = 0.0, dy = 0.0;
	if(type == 0){
		dx = tl + l*2.0 + bl - tr - r*2.0 - br;
		dy = tl + t*2.0 + tr - bl - b*2.0 - br;
	}
	else{
		dx = tl*3.0 + l*10.0 + bl*3.0 - tr*3.0 - r*10.0 - br*3.0;
		dy = tl*3.0 + t*10.0 + tr*3.0 - bl*3.0 - b*10.0 - br*3.0;
	}
	vec4 normal = vec4(normalize(vec3(dx * invertR * invertH * 255.0, dy * invertG * invertH * 255.0, dz)), texture2D(tHeightMap, vUv).a);
	gl_FragColor = vec4(normal.xy * 0.5 + 0.5, normal.zw);

}
</script>

<script type="x-shader/x-fragment" id="tiles-fs" >
precision highp float;

uniform vec2 dimensions;
uniform float seed;

varying vec2 vUv;

// a variant from jt's https://www.shadertoy.com/view/4st3R7
// beside code golfing, main difference is s + S

float hash( float n ) { return fract(sin(n)*753.5453123); }
float noise( in vec3 x )
{
	vec3 p = floor(x);
	vec3 f = fract(x);
	f = f*f*(3.0-2.0*f);

	float n = p.x + p.y*157.0 + 113.0*p.z;
	return mix(mix(mix( hash(n+  0.0), hash(n+  1.0),f.x),
	               mix( hash(n+157.0), hash(n+158.0),f.x),f.y),
	mix(mix( hash(n+113.0), hash(n+114.0),f.x),
	    mix( hash(n+270.0), hash(n+271.0),f.x),f.y),f.z);
}
#define mirror(v) abs(2. * fract(v / 2.) - 1.)

float rnd(vec2 co){
    return fract(sin(dot(co.xy + seed,vec2(12.9898,78.233))) * 43758.5453);
    //return noise( vec3( s, seed ) );
    //texture2D(iChannel0, seed + seed*0.001).x;
}

float len(vec2 v)
{
    float w = 1.25/(.25+mirror(seed*.125));
    return pow(dot(pow(v, vec2(w)), vec2(1)), 1./w);
}

float flip(vec2 v, float w)
{
    v = fract(v / 2.) - .5;
    return mix(w, 1. - w, step(0., v.x * v.y));
}

float tile(vec2 v)
{
    v = fract(v);
    return smoothstep(.0,.05, abs(len(v)-.5)) * smoothstep(.0,.05, abs(len(1.-v)-.5));
}

float tile_full(vec2 v)
{
    v = fract(v);
    return smoothstep(.45,.55, len(v)) * smoothstep(.45,.55, len(1.-v));
}

float pattern(vec2 v)
{
    vec2 id = floor(v);
    return mix(
        flip(v, rnd(id / 128.) < .5 ? 1.-tile_full(v) : tile_full(vec2(1.-v.x,v.y))),
        1.-(rnd(id / 128.) < .5 ? tile(v) : tile(vec2(1.-v.x,v.y))),
        mirror(seed*.01));
}

void main()
{
	vec2 I = vUv;
    float p = 1.-pattern(I*10.);
    gl_FragColor = vec4(p);
    gl_FragColor.rgb *= smoothstep( .45, .55, p ) * vec3( 255., 166., 50. ) / 255.;

}

</script>

<script>

// https://www.shadertoy.com/view/4st3R7

'use strict';

var container, renderer, camera, controls, scene;
var torus, textures;
var mobile = false;//isMobile.any;

var container = document.getElementById( 'container' );

var SHADOW_MAP_WIDTH = 512, SHADOW_MAP_HEIGHT = 512;
/*
document.getElementById( 'refreshBtn' ).addEventListener( 'click', function( e ) {
	refreshTextures();
	e.preventDefault();
});
*/
function addRandomLight( intensity ) {

	var x = Math.random() * 2 - 1
	var z = Math.random() * 2 - 1

	var light = new THREE.SpotLight( 0xffffff, intensity, 0, 1, .5, .5 );
	light.position.set( x, 15, 2 + z );
	light.target.position.set( 0, 0, 0 );

	light.castShadow = true;

	light.shadow = new THREE.LightShadow( new THREE.PerspectiveCamera( 15, 1, 12, 25 ) );
	light.shadow.bias = 0.001;

	light.shadow.mapSize.width = SHADOW_MAP_WIDTH;
	light.shadow.mapSize.height = SHADOW_MAP_HEIGHT;

	scene.add( light );

}

function initScene() {

	var light = new THREE.AmbientLight( 0x101010 );
	scene.add( light );

	var light = new THREE.HemisphereLight( 0xffffff, 0x000000, .5 );
	scene.add( light );

	if( mobile ) {

		addRandomLight( 1 );

	} else {

		for( var j = 0; j < 2; j++ ) {

			addRandomLight( .5 );

		}

	}

	textures = generateTextures( mobile ? 512 : 1024, mobile ? 512 : 1024 );

	textures.diffuseTexture.wrapS = textures.diffuseTexture.wrapT = THREE.RepeatWrapping;
	textures.roughnessTexture.wrapS = textures.roughnessTexture.wrapT = THREE.RepeatWrapping;
	textures.normalTexture.wrapS = textures.normalTexture.wrapT = THREE.RepeatWrapping;
	textures.bumpTexture.wrapS = textures.bumpTexture.wrapT = THREE.RepeatWrapping;

	torus = new THREE.Mesh(
		new THREE.TorusKnotBufferGeometry( 1, .3, 200, 32 ),
		new THREE.MeshStandardMaterial({
			map: textures.diffuseTexture,
			roughness: .5,
			roughnessMap: textures.roughnessTexture,
			bumpMap: textures.bumpTexture,
			bumpScale: .1,
			normalMap: textures.normalTexture,
			normalScale: new THREE.Vector2(.1,.1),
			color: 0xffffff,
			wireframe: !true
		})
	)

	torus.rotation.set(1,1,1);
	torus.castShadow = true
	torus.receiveShadow = true

	scene.add( torus );

}

function generateTextures( width, height ) {

	var diffuseShader = new THREE.RawShaderMaterial( {
		uniforms:{
			type: { type: 'f', value: 0 },
			dimensions: { type: 'v2', value: new THREE.Vector2( width, height ) },
			seed: { type: 'f', value: Math.random() * performance.now() }
		},
		vertexShader: document.getElementById( 'ortho-vs' ).textContent,
		fragmentShader: document.getElementById( 'texture-fs' ).textContent
	} );

	var diffuseTexture = new ShaderTexture( renderer, diffuseShader, width, height );
	diffuseTexture.render();

	var bumpShader = new THREE.RawShaderMaterial( {
		uniforms:{
			type: { type: 'f', value: 1 },
			dimensions: { type: 'v2', value: new THREE.Vector2( width, height ) },
			seed: { type: 'f', value: Math.random() * performance.now() }
		},
		vertexShader: document.getElementById( 'ortho-vs' ).textContent,
		fragmentShader: document.getElementById( 'texture-fs' ).textContent
	} );

	var bumpTexture = new ShaderTexture( renderer, bumpShader, width, height );
	bumpTexture.render();

	var normalShader = new THREE.RawShaderMaterial( {
		uniforms:{
			dz: { type: 'f', value: mobile ? .1 : 10 },
			invertR: { type: 'f', value: 1 },
			invertG: { type: 'f', value: 1 },
			invertH: { type: 'f', value: -1 },
			type: { type: 'f', value: 0 },
			tHeightMap: { type: 't', value: bumpTexture.texture },
			dimensions: { type: 'v2', value: new THREE.Vector2( width, height ) }
		},
		vertexShader: document.getElementById( 'ortho-vs' ).textContent,
		fragmentShader: document.getElementById( 'heightmap-to-normalmap-fs' ).textContent
	} );

	var normalTexture = new ShaderTexture( renderer, normalShader, width, height );
	normalTexture.render();

	var blurNormalShader = new THREE.RawShaderMaterial( {
		uniforms:{
			tInputMap: { type: 't', value: normalTexture.texture },
			dimensions: { type: 'v2', value: new THREE.Vector2( width, height ) },
			sigma: { type: 'f', value: 1 }
		},
		vertexShader: document.getElementById( 'ortho-vs' ).textContent,
		fragmentShader: document.getElementById( 'blur-fs' ).textContent
	} );

	var blurNormalTexture = new ShaderTexture( renderer, blurNormalShader, width, height );
	blurNormalTexture.render();

	var roughnessShader = new THREE.RawShaderMaterial( {
		uniforms:{
			type: { type: 'f', value: 2 },
			dimensions: { type: 'v2', value: new THREE.Vector2( width, height ) },
			seed: { type: 'f', value: Math.random() * performance.now() }
		},
		vertexShader: document.getElementById( 'ortho-vs' ).textContent,
		fragmentShader: document.getElementById( 'texture-fs' ).textContent
	} );

	var roughnessTexture = new ShaderTexture( renderer, roughnessShader, width, height );
	roughnessTexture.render();

	return {
		diffuseTexture: diffuseTexture.texture,
		bumpTexture: bumpTexture.texture,
		roughnessTexture: roughnessTexture.texture,
		normalTexture: blurNormalTexture.texture,
		refresh: refreshTextures
	}

}

function refreshTextures() {

	return;
	textures.refresh();

	textures.texture.repeat.needsUpdate = true;
	textures.roughnessTexture.repeat.needsUpdate = true;
	textures.normalTexture.repeat.needsUpdate = true;

}

function init() {

	container = document.getElementById( 'container' );

	scene = new THREE.Scene();

	scene.fog = new THREE.FogExp2( 0xbfae98, .025 );

	camera = new THREE.PerspectiveCamera( 70, window.innerWidth / window.innerHeight, .1, 100 );
	camera.target = new THREE.Vector3( 0, .7, 0 );
	camera.position.set( 2.5, .5, 1.25 );
	scene.add( camera );

	renderer = new THREE.WebGLRenderer( { antialias: true, preserveDrawingBuffer: true } );
	renderer.setPixelRatio( window.devicePixelRatio );
	renderer.setClearColor( 0xbfae98, 1 );
	container.appendChild( renderer.domElement );

	//addFullscreenShortcut( renderer.domElement, onWindowResized );

	renderer.shadowMap.enabled = true;
	renderer.shadowMap.type = THREE.PCFShadowMap;

	controls = new THREE.OrbitControls( camera, renderer.domElement );

	initScene();
	onWindowResized();

	window.addEventListener( 'resize', onWindowResized );

	window.addEventListener( 'keydown', function( e ) {

		if( e.keyCode === 32 ) {

			refreshTextures();

		}

	});

	animate();

}

function onWindowResized( event ) {

	var w = container.clientWidth;
	var h = container.clientHeight;

	renderer.setSize( w, h );
	camera.aspect = w / h;
	camera.updateProjectionMatrix();

}

function animate() {

	requestAnimationFrame( animate );

	controls.update();
	//torus.rotation.x += .01;
	//torus.rotation.y += .001;

	renderer.render( scene, camera );

}

window.addEventListener( 'load', init );

</script>

	</body>
</html>
